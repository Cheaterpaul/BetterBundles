buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    id 'com.matthewprenger.cursegradle' version '1.2.0'
    id "de.undercouch.download" version "4.1.2"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.0'
group = 'de.cheaterpaul.betterbundles'
archivesBaseName = 'BetterBundles'
ext.mod_version="Test"
ext.type="test"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

apply from: 'gradle/versioning.gradle'
apply from: 'gradle/artifacts.gradle'
apply from: 'gradle/crowdin.gradle'
apply from: 'gradle/curse.gradle'
apply from: 'gradle/forge.gradle'
apply from: 'gradle/maven.gradle'

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Example for how to get properties into the manifest for reading at runtime.
jar {
    dependsOn 'crowdin'
    finalizedBy('reobfJar')

    includeEmptyDirs = false

    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir

    from{
        fileTree('build/translations').matching{
            exclude "**/en_us.json" //Don't override local source file
        }
    }

    manifest {
        attributes([
                "Specification-Title"     : "betterbundles",
                "Specification-Vendor"    : "cheaterpaul",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "Better Bundles",
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "cheaterpaul",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}